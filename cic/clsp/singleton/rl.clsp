(mod
  (
    MOD_HASH
    START_DATE
    STARTING_AMOUNT
    RATE  ; mojos per second
    INNER_PUZZLE
    current_time
    inner_solution
  )

  (include condition_codes.clib)
  (include curry_and_treehash.clib)
  (include sha256tree.clib)
  (include utility_macros.clib) ; or, assert, and

  (defun-inline coin_amount (condition) (f (r (r condition))))
  (defun-inline puzzle_hash (condition) (f (r condition)))

  (defun max (a b)
    (if (> a b)
      a
      b
    )
  )

  (defun-inline morph_singleton_condition
    (
      singleton_condition
      MOD_HASH
      START_DATE
      STARTING_AMOUNT
      RATE
      current_time
    )

    (c
      CREATE_COIN
      (c
        (puzzle_hash_of_curried_function MOD_HASH
          (puzzle_hash singleton_condition)
          (sha256 1 RATE)
          (sha256 1 STARTING_AMOUNT)
          (sha256 1 START_DATE)
          (sha256 1 MOD_HASH)
        )
        (r (r singleton_condition))
      )
    )
  )


  (defun-inline assert_rate (withdrawn_amount possible_withdrawn_amount current_time)
    (assert
      (> possible_withdrawn_amount withdrawn_amount)
      (list ASSERT_SECONDS_ABSOLUTE current_time)
    )
  )

  (defun generate_final_conditions
    (
      singleton_condition
      remaining_conditions
      withdrawn_amount
      possible_withdrawn_amount
      MOD_HASH
      START_DATE
      STARTING_AMOUNT
      RATE
      current_time
    )

    (c
      (assert_rate withdrawn_amount possible_withdrawn_amount current_time)
      (c
        (morph_singleton_condition
          singleton_condition
          MOD_HASH
          START_DATE
          STARTING_AMOUNT
          RATE
        )
        remaining_conditions
      )
    )
  )

  (defun-inline calculate_possible_withdrawal (START_DATE RATE current_time)
    (* RATE (- current_time START_DATE))
  )

  (defun found_singleton
    (
      singleton_condition
      remaining_conditions
      (
        MOD_HASH
        START_DATE
        STARTING_AMOUNT
        RATE
        current_time
      )
    )

    (generate_final_conditions
      singleton_condition
      remaining_conditions
      (- STARTING_AMOUNT (coin_amount singleton_condition))
      (calculate_possible_withdrawal START_DATE RATE current_time)
      MOD_HASH
      START_DATE
      STARTING_AMOUNT
      RATE
      current_time
    )
  )

  (defun search_for_singleton (conditions found_function args)
    (if conditions
        (if (and
              (= (f (f conditions)) CREATE_COIN)      ; this is a CREATE_COIN
              (logand (coin_amount (f conditions)) 1) ; the amount is odd
            )
            (found_function (f conditions) (r conditions) args)
            (c
              (f conditions)
              (search_for_singleton (r conditions) found_function args)
            )
        )
        (x)
    )
  )

  ; main
  (search_for_singleton
    (a INNER_PUZZLE inner_solution)
    found_singleton
    (list
      MOD_HASH
      START_DATE
      STARTING_AMOUNT
      RATE
      current_time
    )
  )
)
